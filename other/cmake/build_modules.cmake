# SPDX-License-Identifier: BSD-2-Clause
cmake_minimum_required(VERSION 3.22)

set(TOOL_WS ${BUILD_DIR}/scripts/weaken_syms)

# --------------------------------------------------------------------
# Sysfs-only special code for the generated config objects

set(
   GENERATED_CONFIG_FILE
   ${BUILD_DIR}/kernel/tilck_gen_headers/generated_config.h
)

set(
   RO_CONFIG_VARS_HEADER
   ${PROJ_ROOT}/modules/sysfs/ro_config_vars.h
)

set(
   ALL_MODULES_LIST_HEADER
   ${BUILD_DIR}/kernel/tilck_gen_headers/all_modules_list.h
)

add_custom_command(

   OUTPUT
      ${GENERATED_CONFIG_FILE}

   COMMAND
      ${BUILD_APPS}/gen_config ${PROJ_ROOT} ${GENERATED_CONFIG_FILE}

   DEPENDS
      ${RO_CONFIG_VARS_HEADER}
      ${ALL_MODULES_LIST_HEADER}
      ${BUILD_APPS}/gen_config
)

add_custom_target(

   generated_configuration

   DEPENDS
      ${RO_CONFIG_VARS_HEADER}
      ${ALL_MODULES_LIST_HEADER}
      ${GENERATED_CONFIG_FILE}
)

# ----------------------------------------------------------------------

#
# Internal macro use by the build_and_link_module() function
#

macro(__build_and_link_module_patch_logic)

   set(PATCHED_MOD_FILE "libmod_${modname}_patched.a")

   if (APPLE)
      set(_ws_extra_deps machohack)
   else()
      set(_ws_extra_deps elfhack32 elfhack64)
   endif()

   add_custom_command(

      OUTPUT
         ${PATCHED_MOD_FILE}
      COMMAND
         cp libmod_${modname}${variant}.a ${PATCHED_MOD_FILE}
      COMMAND
         ${TOOL_WS} ${PATCHED_MOD_FILE} ${WRAPPED_SYMS}
      DEPENDS
         mod_${modname}${variant}
         ${TOOL_WS}
         ${_ws_extra_deps}
      COMMENT
         "Patching the module ${modname} to allow wrapping of symbols"
      VERBATIM
   )

   add_custom_target(
      mod_${modname}_patched
      DEPENDS ${PATCHED_MOD_FILE}
   )

endmacro()

#
# Build and statically link a kernel module
#
# ARGV0: target
# ARGV1: module name
# ARGV2: special flag: If equal to "_noarch", don't build the arch code.
# ARGV3: patch flag: run the WS_TOOL to weaken all the symbols in the static
#        archive if the flag is true.
#

function(build_and_link_module target modname)

   set(variant "${ARGV2}")
   set(DO_PATCH "${ARGV3}")
   set(MOD_${modname}_SOURCES_GLOB "")

   # message(STATUS "build_module(${target} ${modname} ${variant})")

   list(
      APPEND MOD_${modname}_SOURCES_GLOB
      "${PROJ_ROOT}/modules/${modname}/*.c"
      "${PROJ_ROOT}/modules/${modname}/*.cpp"
   )

   if (NOT "${variant}" STREQUAL "_noarch")

      if (NOT "${variant}" STREQUAL "")
         message(FATAL_ERROR "Flag must be \"_noarch\" or empty.")
      endif()

      list(
         APPEND MOD_${modname}_SOURCES_GLOB
         "${PROJ_ROOT}/modules/${modname}/${ARCH}/*.c"
         "${PROJ_ROOT}/modules/${modname}/${ARCH_FAMILY}/*.c"
      )
   endif()

   file(
      GLOB
      MOD_${modname}_SOURCES         # Output variable
      ${GLOB_CONF_DEP}               # The CONFIGURE_DEPENDS option
      ${MOD_${modname}_SOURCES_GLOB} # The input GLOB text
   )

   # It's totally possible that some modules contain exclusively arch-only
   # code. In that case, the list of sources will be empty when the flag
   # "noarch" is passed and we just won't create any target.

   if (MOD_${modname}_SOURCES)

      add_library(
         mod_${modname}${variant} STATIC EXCLUDE_FROM_ALL
         ${MOD_${modname}_SOURCES}
      )

      if (${modname} STREQUAL "sysfs")
         add_dependencies(mod_${modname}${variant} generated_configuration)
      endif()

      if ("${variant}" STREQUAL "_noarch")

         set_target_properties(

            mod_${modname}${variant}

            PROPERTIES
               COMPILE_FLAGS "${KERNEL_NO_ARCH_FLAGS}"
         )

         if (DO_PATCH)
            __build_and_link_module_patch_logic()
         endif(DO_PATCH)

      else()

         set_target_properties(

            mod_${modname}${variant}

            PROPERTIES
               COMPILE_FLAGS "${KERNEL_FLAGS} ${ACTUAL_KERNEL_ONLY_FLAGS}"
         )

      endif()

      # Link the patched or the regular module version

      if (DO_PATCH)

         add_dependencies(${target} mod_${modname}_patched)

         target_link_libraries(
            ${target} ${CMAKE_CURRENT_BINARY_DIR}/${PATCHED_MOD_FILE}
         )

      else()
         target_link_libraries(${target} mod_${modname}${variant})
      endif()

   endif(MOD_${modname}_SOURCES)
endfunction()

#
# Build and link all modules to a given target
#
function(build_all_modules TARGET_NAME)

   set(TARGET_VARIANT "${ARGV1}")
   set(DO_PATCH "${ARGV2}")
   if (APPLE)
      # macOS ld uses -force_load per archive; handled at link time
   else()
      target_link_libraries(${TARGET_NAME} -Wl,--whole-archive)
   endif()

   foreach (mod ${modules_list})

      if ("${TARGET_VARIANT}" STREQUAL "_noarch")
         list(FIND no_arch_modules_whitelist ${mod} _index)
         if (${_index} EQUAL -1)
            continue()
         endif()
      else()
         # Even if it's ugly, check here if the module should be compiled-in
         # or not. In the "noarch" case, always compile the modules in, because
         # they are needed for unit tests.
         if (NOT MOD_${mod})
            continue()
         endif()
      endif()

      if (EXISTS ${PROJ_ROOT}/modules/${mod}/${mod}.cmake)

         # Use the custom per-module CMake file
         include(${PROJ_ROOT}/modules/${mod}/${mod}.cmake)

      else()

         # Use the generic build & link code
         build_and_link_module(
            ${TARGET_NAME} ${mod} ${TARGET_VARIANT} ${DO_PATCH}
         )

      endif()

   endforeach()

   if (NOT APPLE)
      target_link_libraries(${TARGET_NAME} -Wl,--no-whole-archive)
   endif()
endfunction()
